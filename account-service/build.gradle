plugins {
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'java'
    id 'groovy'
    id 'net.ltgt.apt-eclipse' version '0.21'
    id 'net.ltgt.apt-idea' version '0.21'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id 'application'
    id 'nu.studer.jooq' version '3.0.3'
}

version '0.1.0-SNAPSHOT'
group 'com.revolut'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven { url 'https://jcenter.bintray.com' }
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.1.3'
    }
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'io.micronaut:micronaut-inject-java'
    annotationProcessor 'io.micronaut:micronaut-validation'
    annotationProcessor 'io.micronaut.configuration:micronaut-openapi'
    compile 'io.micronaut:micronaut-inject'
    compile 'io.micronaut:micronaut-validation'
    compile 'io.micronaut:micronaut-runtime'
    compile 'io.micronaut:micronaut-management'
    compile 'javax.annotation:javax.annotation-api'
    compile 'io.micronaut.configuration:micronaut-hibernate-validator'
    compile 'io.micronaut:micronaut-http-server-netty'
    compile 'io.micronaut.configuration:micronaut-jdbc-hikari'
    compile 'io.micronaut.configuration:micronaut-flyway'
    compile 'io.micronaut:micronaut-spring'
    compile 'org.jooq:jooq'
    compile 'org.projectlombok:lombok:1.18.8'
    compile 'com.h2database:h2'
    compile 'io.swagger.core.v3:swagger-annotations'
    runtime 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
    jooqRuntime 'org.jooq:jooq-meta-extensions'
}

mainClassName = 'com.revolut.account.Application'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

shadowJar {
    mergeServiceFiles()
}

run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')

sourceSets.main.java.srcDirs = ['src/main/java', 'src/generated/java']

jooq {
    version = '3.11.11'
    account(sourceSets.main) {
        generator {
            database {
                name = 'org.jooq.meta.extensions.ddl.DDLDatabase'
                properties {
                    property {
                        key = 'scripts'
                        value = 'src/main/resources/db/migration/'
                    }
                    property {
                        key = 'sort'
                        value = 'semantic'
                    }
                }
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = false
                fluentSetters = false
            }
            target {
                packageName = 'com.revolut.account.jooq'
                directory = 'src/generated/java'
            }
        }
    }
}

project.tasks.getByName('compileJava').dependsOn -= 'generateAccountJooqSchemaSource'